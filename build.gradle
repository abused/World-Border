buildscript
{
    // Repositories and plugins used by this buildscript. For internal Gradle use only.
    repositories
    {
        mavenCentral()
        maven
        {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven
        {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

// Applies the custom Gradle plugin Forge uses, for Forge-specific tasks
apply plugin: 'net.minecraftforge.gradle.forge'

// Gradle project metadata
version = "1.0.3"
group   = "com.wimbli.WorldBorder"

minecraft
{
    // Forge version
    version = "1.10.2-12.18.3.2511"
    runDir  = "debug"
	
	mappings = "snapshot_20161111"
}

// Sets the output directory of the mod's jar file
jar.destinationDir = file 'output'

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

// Fixes issues with debugging in IntelliJ
idea.module.inheritOutputDirs = true

// Fixes issues with unicode in source files
// From https://discuss.gradle.org/t/special-characters-in-java-string-literals-arent-interpreted-correctly/4665
tasks.withType(JavaCompile)
{
    options.encoding = "UTF-8"
}

repositories
{
    maven
    {
        name = "mikeprimm"
        url  = "http://repo.mikeprimm.com/"
    }
}

dependencies
{
    //compile     'org.dynmap:DynmapCoreAPI:1.9.4'
    testCompile 'junit:junit:4.11'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'worldborder_at.cfg'
    }
}
